plugins {
    id 'java'
    id "xyz.jpenilla.run-waterfall" version "2.3.1"
    id "com.gradleup.shadow" version "8.3.6"
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } // This lets gradle find the BungeeCord files online
    maven {
        url = "https://repo.codemc.io/repository/maven-releases/"
    }
    maven {
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    compileOnly 'net.md-5:bungeecord-api:1.21-R0.3-SNAPSHOT'
    implementation "com.github.retrooper:packetevents-bungeecord:2.7.0"
    compileOnly 'org.slf4j:slf4j-api:2.0.9'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 21 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks {
    runWaterfall {
        // Configure the Waterfall version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        waterfallVersion("1.21")
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.shadowJar {
    archiveClassifier.set('all')
    archiveBaseName.set("bundlecrashfix-bungeecord")
    minimize()
    relocate 'com.github.retrooper.packetevents', 'rs.onako2.bundlecrashfix.retrooper.packetevents'
}

tasks.build {
    dependsOn tasks.shadowJar
}
